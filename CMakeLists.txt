CMAKE_MINIMUM_REQUIRED(VERSION 3.1.0 FATAL_ERROR)

SET(PACKAGE_NAME      "ibus-unikey")

PROJECT(${PACKAGE_NAME} LANGUAGES CXX)

ADD_COMPILE_OPTIONS(-std=c++11 -Wall -Werror -g -O3 -pedantic -march=native -fPIC)

SET(CMAKE_MODULE_PATH "${PROJECT_SOURCE_DIR}/cmake")
MESSAGE(STATUS "CMake module path: ${CMAKE_MODULE_PATH}")

##################################################################
# Versioning from GIT
INCLUDE(Utilities)

FIND_PACKAGE(Git)
GetAndParseVersion()

SET(PACKAGE_STRING    "${PACKAGE_NAME} ${PACKAGE_VERSION}")
SET(PACKAGE_TARNAME   "${PACKAGE_NAME}-${PACKAGE_VERSION}")
SET(PACKAGE_BUGREPORT "https://github.com/ibus-unikey/ibus-unikey")

# ----- DEPENDENCIES ----- #
INCLUDE(GNUInstallDirs)

FIND_PACKAGE(PkgConfig)
PKG_CHECK_MODULES(IBUS REQUIRED ibus-1.0)

SET(THREADS_PREFER_PTHREAD_FLAG ON)
FIND_PACKAGE(Threads REQUIRED)
FIND_PACKAGE(GTK2 REQUIRED
    COMPONENTS
    gtk
)

FIND_PACKAGE(Gettext REQUIRED)

INCLUDE_DIRECTORIES(${IBUS_INCLUDE_DIRS})
INCLUDE_DIRECTORIES(${GTK2_INCLUDE_DIRS})
INCLUDE_DIRECTORIES(BEFORE ${PROJECT_SOURCE_DIR}/ukengine)
INCLUDE_DIRECTORIES(BEFORE ${PROJECT_SOURCE_DIR}/src)

SET(LIBEXECDIR          "${CMAKE_INSTALL_FULL_LIBEXECDIR}/${PACKAGE_NAME}")
SET(LOCALEDIR           "${CMAKE_INSTALL_FULL_LOCALEDIR}")
SET(PKGDATADIR          "${CMAKE_INSTALL_FULL_DATADIR}/${PACKAGE_NAME}")
SET(UI_DATA_DIR         "${PKGDATADIR}/ui")
SET(IBUS_COMPONENT_DIR  "${CMAKE_INSTALL_FULL_DATADIR}/ibus/component")

CONFIGURE_FILE(
  "${PROJECT_SOURCE_DIR}/src/unikey.xml.in"
  "${CMAKE_CURRENT_BINARY_DIR}/unikey.xml"
)

# ----- SOURCES ----- #

FILE(GLOB UKENGINE_SRC
  "ukengine/*.cpp"
)

FILE(GLOB IBUS_UNIKEY_ENGINE_SRC
  "src/*.cpp"
)

FILE(GLOB IBUS_UNIKEY_SETUP_SRC
  "setup/*.cpp"
  "src/utils.cpp"
)

FILE(GLOB UI_SETUP_FILES
  "setup/*.ui"
)

FILE(GLOB IBUS_COMPONENT_FILES
  "${CMAKE_CURRENT_BINARY_DIR}/unikey.xml"
)

FILE(GLOB IBUS_UNIKEY_ICONS_FILES
  "icons/ibus-unikey.png"
)

FILE(GLOB IBUS_UNIKEY_PO_FILES
  "po/vi.po"
)

# ------ LIBRARIES --------#

ADD_LIBRARY(libunikey-ibus STATIC ${UKENGINE_SRC})

TARGET_COMPILE_OPTIONS(libunikey-ibus
  PUBLIC -funsigned-char)

# ------ EXECUTABLES --------#

# ------ ibus-engine-unikey --------#
ADD_EXECUTABLE(ibus-engine-unikey ${IBUS_UNIKEY_ENGINE_SRC})

TARGET_LINK_LIBRARIES(ibus-engine-unikey
  libunikey-ibus
  ${IBUS_LIBRARIES}
  Threads::Threads
)

TARGET_COMPILE_OPTIONS(ibus-engine-unikey
  PUBLIC -Wno-variadic-macros
)

TARGET_COMPILE_DEFINITIONS(ibus-engine-unikey
  PUBLIC LIBEXECDIR=\"${LIBEXECDIR}\"
  PUBLIC GETTEXT_PACKAGE=\"${PACKAGE_NAME}\"
  PUBLIC PACKAGE_STRING=\"${PACKAGE_STRING}\"
  PUBLIC LOCALEDIR=\"${LOCALEDIR}\"
  PUBLIC PACKAGE_VERSION=\"${PACKAGE_VERSION}\"
  PUBLIC PACKAGE_BUGREPORT=\"${PACKAGE_BUGREPORT}\"
  PUBLIC PACKAGE_NAME=\"${PACKAGE_NAME}\"
  PUBLIC PKGDATADIR=\"${PKGDATADIR}\"
)

# ------ ibus-setup-unikey --------#
ADD_EXECUTABLE(ibus-setup-unikey ${IBUS_UNIKEY_SETUP_SRC})

TARGET_LINK_LIBRARIES(ibus-setup-unikey
  libunikey-ibus
  ${IBUS_LIBRARIES}
  ${GTK2_LIBRARIES}
)

TARGET_COMPILE_OPTIONS(ibus-setup-unikey
  PUBLIC -Wno-variadic-macros
)

TARGET_COMPILE_DEFINITIONS(ibus-setup-unikey
  PUBLIC LIBEXECDIR=\"${LIBEXECDIR}\"
  PUBLIC PKGDATADIR=\"${PKGDATADIR}\"
  PUBLIC UI_DATA_DIR=\"${UI_DATA_DIR}\"
  PUBLIC LOCALEDIR=\"${LOCALEDIR}\"
  PUBLIC GETTEXT_PACKAGE=\"${PACKAGE_NAME}\"
  PUBLIC PACKAGE_STRING=\"${PACKAGE_STRING}\"
  PUBLIC PACKAGE_VERSION=\"${PACKAGE_VERSION}\"
  PUBLIC PACKAGE_BUGREPORT=\"${PACKAGE_BUGREPORT}\"
  PUBLIC PACKAGE_NAME=\"${PACKAGE_NAME}\"
)

# ----- install -------#

INSTALL(TARGETS ibus-engine-unikey ibus-setup-unikey
  DESTINATION ${LIBEXECDIR})

INSTALL(FILES ${UI_SETUP_FILES}
  DESTINATION ${UI_DATA_DIR})

INSTALL(FILES ${IBUS_COMPONENT_FILES}
  DESTINATION ${IBUS_COMPONENT_DIR})

INSTALL(FILES ${IBUS_UNIKEY_ICONS_FILES}
  DESTINATION ${PKGDATADIR}/icons)

GETTEXT_PROCESS_PO_FILES(vi ALL PO_FILES ${IBUS_UNIKEY_PO_FILES})

INSTALL(FILES "${CMAKE_CURRENT_BINARY_DIR}/vi.gmo"
  DESTINATION "${LOCALEDIR}/vi/LC_MESSAGES"
  RENAME "${PACKAGE_NAME}.mo")

ADD_SUBDIRECTORY(po)